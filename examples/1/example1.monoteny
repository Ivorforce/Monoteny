-- import(monoteny.dialects.common.0.0);

-- Make a pattern: `a /_ b` calls `floor_div(lhs: <>, rhs: <>)`
pattern floor_div(MultiplicationPrecedence) :: {lhs} /_ {rhs};

-- Define a function floor_div(lhs '$Float, rhs: $Float) using the pattern we set before.
def {$Float} /_ {$Float} -> $Float :: lhs / rhs;

-- Define a function on numbers.
def {$Number}.square() -> $Number :: self * self;

-- Define a 'static' function on numbers.
def square(value: '$Number) -> $Number :: value.square();

-- Define pi_ish as an overloaded constant. For floats, it's 3.14. For ints, it's 3.
-- Depending on the caller, using this 'constant' is going to yield different values.
def pi_ish -> $Float :: 3.14;
def pi_ish -> $Int :: 3;

@main
def main() :: {
    -- Print string
    print("Test: ");

    -- 'value' is not bound yet. It could be any $Float!
    let value = 2.2.square() * log(e);

    -- Bind 'value' implicitly by binding b to Float32.
    var b 'Float32 = square(value:) + pi_ish;
    upd b = b.square() ** -2.2 /_ value + tau;

    -- Use pi_ish for ints, which yields 3.
    var c 'Int32 = pi_ish;

    -- Print boolean
    print(b < 2 and not (value > 2) and true);
}

@transpile
def transpile(transpiler 'Transpiler) :: {
    -- The idea is that if we transpile this and run it, it should yield the same output
    -- as if running using the interpreter.

    -- Ideally, constant folding can break it down to just print statements.
    transpiler.add(main);
}
