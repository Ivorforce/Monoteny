use lalrpop_util::ErrorRecovery;
use std::str::FromStr;
use itertools::Itertools;
use crate::util::position::*;
use crate::parser::ast::*;
use crate::parser::lexer::*;
use crate::parser::error::*;
use crate::program::types::*;
use crate::program::allocation::*;
use crate::program::functions::ParameterKey;

grammar<'i, 'err>(errors: &'err mut Vec<ErrorRecovery<usize, Token<'i>, Error>>, text: &'i str);

extern {
    type Location = usize;
    type Error = Error;

    enum Token<'i> {
        _Identifier => Token::Identifier(_),
        _MacroIdentifier => Token::MacroIdentifier(_),
        _OperatorIdentifier => Token::OperatorIdentifier(_),
        _IntLiteral => Token::IntLiteral(_),
        _RealLiteral => Token::RealLiteral(_),
        StringLiteral => Token::StringLiteral(_),

        "def" => Token::Symbol("def"),
        "trait" => Token::Symbol("trait"),
        "declare" => Token::Symbol("declare"),

        "!" => Token::Symbol("!"),
        "{" => Token::Symbol("{"),
        "}" => Token::Symbol("}"),
        "(" => Token::Symbol("("),
        ")" => Token::Symbol(")"),
        "[" => Token::Symbol("["),
        "]" => Token::Symbol("]"),
        "::" => Token::Symbol("::"),
        ":" => Token::Symbol(":"),
        "@" => Token::Symbol("@"),
        "'" => Token::Symbol("'"),
        "=" => Token::Symbol("="),
        "," => Token::Symbol(","),
        ";" => Token::Symbol(";"),
        "." => Token::Symbol("."),
        "->" => Token::Symbol("->"),
        "\"" => Token::Symbol("\""),

        "let" => Token::Symbol("let"),
        "var" => Token::Symbol("var"),
        "upd" => Token::Symbol("upd"),
        "return" => Token::Symbol("return"),

        "is" => Token::Symbol("is"),
        "if" => Token::Symbol("if"),
        "else" => Token::Symbol("else"),
    }
}

// =============================== Primitive =====================================

Identifier: String = { _Identifier => <>.to_string() };
MacroIdentifier: String = { _MacroIdentifier => <>.to_string() };
OperatorIdentifier: String = { _OperatorIdentifier => <>.to_string(), };
IntLiteral: String = { _IntLiteral => <>.to_string() };
RealLiteral: String = { _RealLiteral => <>.to_string() };

AnyIdentifier: String = {
    Identifier,
    OperatorIdentifier,
};


// =============================== Global =====================================


pub Block: Block = {
    <statements: Box<Decorated<Positioned<(<Statement> ";")>>>*> => Block { <> }
};

Struct: Struct = {
    "(" <arguments: OptionalFinalSeparatorList<Box<Positioned<StructArgument>>, ",">> ")" => Struct { <> }
}

StructArgument: StructArgument = {
    <key: Identifier> ":" <value: Expression> <type_declaration: ("'" <Expression>)?> => StructArgument { key: ParameterKey::Name(key), value: value, type_declaration },
    <value: Expression> <type_declaration: ("'" <Expression>)?> => StructArgument { key: ParameterKey::Positional, value: value, type_declaration },
    <start:@L> <key: Identifier> <end:@R> ":" <type_declaration: ("'" <Expression>)?> => StructArgument { key: ParameterKey::Name(key.clone()), value: Expression::from(vec![Box::new(positioned(Term::Identifier(key.clone()), start, end))]), type_declaration: type_declaration },
}

Array: Array = {
    "[" <arguments: OptionalFinalSeparatorList<Box<Positioned<ArrayArgument>>, ",">> "]" => Array { <> }
}

ArrayArgument: ArrayArgument = {
    <key: Expression> ":" <value: Expression> <type_declaration: ("'" <Expression>)?> => ArrayArgument { key: Some(key), value, type_declaration },
    <value: Expression> <type_declaration: ("'" <Expression>)?>=> ArrayArgument { key: None, value, type_declaration },
}

Function: Function = {
    "def" <interface: FunctionInterface> <body: ("::" <FunctionBody>)?> => Function { <> },
}

FunctionInterface: FunctionInterface = {
    <expression: Expression> <return_type: ("->" <Expression>)?> => FunctionInterface { <> },
}

FunctionBody: Expression = {
    <Expression>,
}

// =============================== Trait =====================================

Trait: TraitDefinition = {
    "trait" <name: Identifier> "{" <block: Box<Block>> "}" => TraitDefinition { <> },
}

Conformance: TraitConformanceDeclaration = {
    "declare" <declared_for: Expression> "is" <declared: Identifier> "{" <block: Box<Block>> "}" => TraitConformanceDeclaration { <> },
}

// =============================== Statement =====================================


Statement: Statement = {
    <mutability: VariableDeclarationMutability> <identifier: Identifier> <type_declaration: ("'" <Box<Expression>>)?> <assignment: ("=" <Box<Expression>>)?> => Statement::VariableDeclaration { mutability, identifier, type_declaration, assignment },
    "upd" <target: Box<Expression>> "=" <new_value: Box<Expression>> => Statement::VariableUpdate { <> },
    "return" <Box<Expression>?> => Statement::Return(<>),
    Box<Expression> => Statement::Expression(<>),
    Box<Function> => Statement::FunctionDeclaration(<>),
    Box<Trait> => Statement::Trait(<>),
    Box<Conformance> => Statement::Conformance(<>),
}

VariableDeclarationMutability: Mutability = {
    "let" => Mutability::Immutable,
    "var" => Mutability::Mutable,
}

// =============================== Expression =====================================

Expression: Expression = {
    Box<Positioned<IfThenElseTerm>> => Expression::from(vec![<>]),
    ExpressionNoIfThenElse,
}

IfThenElseTerm: Term = {
    "if" <condition: ExpressionNoIfThenElse> "::" <consequent: ExpressionNoIfThenElse> <alternative: ("else" "::" <Expression>)?> => Term::IfThenElse(Box::new(IfThenElse { <> })),
}

ExpressionNoIfThenElse: Expression = {
    Box<Positioned<Term>>+ => Expression::from(<>),
}

Term: Term = {
    AnyIdentifier => Term::Identifier(<>),
    MacroIdentifier => Term::MacroIdentifier(<>),
    IntLiteral => Term::IntLiteral(<>),
    RealLiteral => Term::RealLiteral(<>),
    "." => Term::Dot,
    Box<Array> => Term::Array(<>),
    Box<Struct> => Term::Struct(<>),
    "\"" <Box<Positioned<StringPart>>*> "\"" => Term::StringLiteral(<>),
    "{" <Box<Block>> "}" => Term::Block(<>),
    <start:@L> <e: !> <end:@R> => { errors.push(e.clone()); Term::Error(derive_error(&e, start, end)) },
}

StringPart: StringPart = {
    StringLiteral => StringPart::Literal(<>.to_string()),
    Box<Struct> => StringPart::Object(<>),
}

// =============================== Util =====================================

Box<Element>: Box<Element> = {
    Element => Box::new(<>),
}

Positioned<Element>: Positioned<Element> = {
    <start: @L> <v: Element> <end: @R> => positioned(v, start, end),
}

Decorated<Element>: Decorated<Element> = {
    <decorations: ("!" <Array>)?> <value: Element> => Decorated { decorations: decorations.unwrap_or(Array { arguments: vec![] }), value },
}

OptionalFinalSeparatorList<Element, Separator>: Vec<Element> = {
    <v0: (<Element> Separator)*> <e1: Element?> => v0.into_iter().chain(e1).collect()
}
